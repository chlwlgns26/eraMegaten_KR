
;イベントフラグ:80:0 = イベント閲覧チェック（未体験=0体験済=1）

;====================
;条件
;====================
@PLAYABLE_EVENT_81
RESULTS = 文章テスト
	RETURN 1


;====================
;内容
;====================
@SHOPCOM_EVENT_81

PRINTW 技ＣＳＶ
CALL 文章テスト_81
;CALL 文章テスト_81
;CALL 文章テスト_81
;CALL 文章テスト_81

@文章テスト_81
PRINTFORML スキル番号,相性,種類,名前,威力,期待・最大値,命中,攻撃回数,一体最大,範囲,射程,タイプ,コスト,対象,付与異常,付与相性,付与率,ランク,ランクアップ先
FOR LOCAL,1,2500
	SIF LOCAL == 2101
		LOCAL = 2400
	TRYCCALLFORM SKILL_NAME_{LOCAL}
		PRINTFORM {LOCAL},
		RESULT = 0
		
		TRYCALLFORM SKILL_SUCCESSION_TYPE_{LOCAL}
		PRINTFORM %GET_SUCCESSION(RESULT)%,
		RESULT = 0
		
		TRYCALLFORM SKILL_EFECT_{LOCAL}
		SELECTCASE RESULT
			CASE 1
				PRINTFORM 攻撃
			CASE 2
				PRINTFORM 回復
			CASE 3
				PRINTFORM 状態変化
			CASE 4
				PRINTFORM 特殊
			CASE 5
				PRINTFORM 状態回復
			CASEELSE
				PRINTFORM その他
		ENDSELECT
		PRINT ,
		RESULT = 0
		
		PRINTFORM %RESULTS%,
		
		TRYCALLFORM SKILL_POWER_{LOCAL}
		PRINTFORM {RESULT},
		
		LOCAL:10 = RESULT
		TRYCCALLFORM SKILL_MINATTACKNUMBER_{LOCAL},-1
			;PRINTFORM 攻撃回数:{RESULT}〜
			LOCAL:11 = RESULT
			CALLFORM SKILL_MAXATTACKNUMBER_{LOCAL},-1
			;PRINTFORM {RESULT}
			LOCAL:12 = RESULT
			TRYCCALLFORM SKILL_MAXATK_PER_{LOCAL},-1
				SIF LOCAL:12 > RESULT
					LOCAL:12 = RESULT
				SIF LOCAL:11 > RESULT
					LOCAL:11 = RESULT
				;PRINTFORM 　1体当たり{RESULT}HITまで
			CATCH
			ENDCATCH
		CATCH
			LOCAL:11 = 1
			LOCAL:12 = 1
		ENDCATCH
		LOCAL:13 = (LOCAL:11 + LOCAL:12)*100/2
		LOCAL:14 = LOCAL:10 * LOCAL:13/100
		PRINTFORM {LOCAL:14},
		RESULT = 0
		
		TRYCALLFORM SKILL_HITRATE_{LOCAL}
		PRINTFORM {RESULT},
		RESULT = 0
		
		TRYCCALLFORM SKILL_MINATTACKNUMBER_{LOCAL},-1
			LOCAL:11 = RESULT
			CALLFORM SKILL_MAXATTACKNUMBER_{LOCAL},-1
			IF LOCAL:11 == RESULT
				PRINTFORM {RESULT},
			ELSE
				PRINTFORM {LOCAL:11}-{RESULT},
			ENDIF
		CATCH
			PRINTFORM 1,
		ENDCATCH
		RESULT = 0
		
		TRYCCALLFORM SKILL_MAXATK_PER_{LOCAL},-1
			PRINTFORM {RESULT},
		CATCH
			TRYCCALLFORM SKILL_MAXATTACKNUMBER_{LOCAL},-1
				PRINTFORM {RESULT},
			CATCH
				PRINTFORM 1,
			ENDCATCH
		ENDCATCH
		RESULT = 0
		
		TRYCALLFORM SKILL_SPHERE_{LOCAL}
		SELECTCASE RESULT
			CASE 1
				PRINTFORM 単体
			CASE 2
				PRINTFORM 列
			CASE 3
				PRINTFORM 全体
			CASE 4
				PRINTFORM 敵味方
			CASEELSE
				PRINT なし
		ENDSELECT
		PRINT ,
		RESULT = 0
		
		TRYCALLFORM SKILL_RANGE_{LOCAL}
		SELECTCASE RESULT
			CASE IS < 1
				PRINTFORM なし
			CASE 1
				PRINTFORM 近
			CASE 2
				PRINTFORM 中
			CASE IS > 2
				PRINTFORM 遠
		ENDSELECT
		PRINT ,
		RESULT = 0
				
		TRYCALLFORM SKILL_DAMAGETYPE_{LOCAL}
		SELECTCASE RESULT
			CASE 1
				PRINTFORM 物理
			CASE 2
				PRINTFORM 魔法
			CASEELSE
				PRINT なし
		ENDSELECT
		PRINT ,
		RESULT = 0
		
		
		TRYCALLFORM SKILL_COST_{LOCAL}
		LOCAL:1 = RESULT
		RESULT = 0
		TRYCALLFORM SKILL_COSTTYPE_{LOCAL}
		IF RESULT == 3
			PRINTFORM {LOCAL:1},
		ELSEIF RESULT == 2
			PRINTFORM HP{LOCAL:1}％,
		ELSE
			PRINTFORM なし,
		ENDIF
		RESULT = 0
		
		TRYCALLFORM SKILL_TARGET_{LOCAL}
		SELECTCASE RESULT
			CASE 1
				PRINTFORM 敵
			CASE 2
				PRINTFORM 味方
			CASE 3
				PRINTFORM 場全体
			CASEELSE
				PRINTFORM 特殊
		ENDSELECT
		PRINT ,
		RESULT = 0
		
		TRYCALLFORM SKILL_ADDTIONAL_STATE_{LOCAL}
		IF RESULT == 0
			PRINTFORM なし,なし,なし,
		ELSE
			PRINTFORM %GET_STATE(RESULT)%,
			
			TRYCALLFORM SKILL_ADDTIONAL_TYPE_{LOCAL}
			PRINTFORM %GET_TYPE(RESULT)%,
			
			TRYCALLFORM SKILL_ADDTIONAL_BASE_RATE_{LOCAL}
			PRINTFORM {RESULT},
		ENDIF
		RESULT = 0
		
		TRYCALLFORM SKILL_RANK_{LOCAL}		
		PRINTFORM {RESULT},
		RESULT = 0
		
		TRYCCALLFORM SKILL_RANKUP_{LOCAL}
			IF RESULT == 0
				PRINTFORM なし,
			ELSE
				FOR LOCAL:1,1,(RESULT+1)
					CALLFORM SKILL_NAME_{D:(LOCAL:1)}
					PRINTFORM %RESULTS%,
				NEXT
			ENDIF
		CATCH
			PRINTFORM なし,
		ENDCATCH
		RESULT = 0
		
		PRINTL 
	CATCH
	ENDCATCH
NEXT
WAIT
